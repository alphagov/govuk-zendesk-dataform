config {
    type: "incremental"
}

SELECT
  CAST(id AS STRING) AS id,
  CAST(created_at AS TIMESTAMP) AS created_at,
  CAST(updated_at AS TIMESTAMP) AS updated_at,
  CAST(JSON_VALUE(ticket,'$.type') AS STRING) AS type,
  CAST(JSON_VALUE(ticket,'$.subject') AS STRING) AS subject,
  CAST(JSON_VALUE(ticket,'$.description') AS STRING) AS description,
  CAST(JSON_VALUE(ticket,'$.priority') AS STRING) AS priority,
  CAST(JSON_VALUE(ticket,'$.status') AS STRING) AS status,
  CAST(JSON_VALUE(ticket,'$.assignee_id') AS STRING) AS assignee_id,
  CAST(JSON_VALUE(ticket,'$.requester_id') AS STRING) AS requester_id,
  CAST(JSON_VALUE(ticket,'$.submitter_id') AS STRING) AS submitter_id,
  CAST(JSON_VALUE(ticket,'$.recipient') AS STRING) AS recipient,
  CAST(JSON_VALUE(ticket,'$.organization_id') AS STRING) AS organization_id,
  CAST(JSON_VALUE(ticket,'$.group_id') AS STRING) AS group_id,
  CAST(JSON_VALUE(ticket,'$.due_at') AS STRING) AS due_at,
  ticket.tags
FROM
  ${ref("tickets")}
WHERE
  updated_at > updated_at_timestamp_checkpoint

pre_operations {
  DECLARE
    updated_at_timestamp_checkpoint DEFAULT (
    ${
        when(incremental(),
            `SELECT max(updated_at) FROM ${self()}`,
            `SELECT TIMESTAMP_SUB(CURRENT_TIMESTAMP(), INTERVAL 365 DAY)`)
    }
    )
}
